module(
    name = "gba_example",
    version = "0.0.1",
)

# --------------------------------------------------------------------------------------------------
# MARK: BCR Dependencies
#
# These dependencies come from the Bazel Central Registry (aka BCR, https://registry.bazel.build),
# or from overrides below.

bazel_dep(name = "bazel_skylib", version = "1.9.0")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.2.17")
bazel_dep(name = "rules_go", version = "0.60.0")
bazel_dep(name = "rules_shell", version = "0.6.1")

bazel_dep(name = "wolfd_bazel_compile_commands", version = "0.5.2", dev_dependency = True)

# --------------------------------------------------------------------------------------------------
# MARK: Toolchains

register_toolchains(
    "//platform:toolchain",
)

# --------------------------------------------------------------------------------------------------
# MARK: devkitARM
#
# Gets 403 forbidden on devkitpro servers.
# Because of that issue, we cannot fetch any dependencies from their repo.
# Waiting for a long term solution, I manually committed the tarballs to the repo and used a custom rule
# `new_tarball_dependency` to extract them to the sandbox.
# Rewind the git history to get the initial intended implementation.
# TODO: try to inject user agents to avoid 403.

new_tarball_dependency = use_repo_rule("//deps/tarballs:new_tarball_dependency.bzl", "new_tarball_dependency")

# Initially fetched by //platform:repo.bzl.
# TODO: handle other OS/arch. I only support darwin package at the moment to avoid inflating the git, devkitARM being large (~50MiB).
new_tarball_dependency(
    name = "devkitarm",
    build_file = "//deps:devkitarm.bazel",
    path = "//deps/tarballs:darwin-devkitARM-r58-2-x86_64.pkg.tar.xz",
    strip_prefix = "opt/devkitpro/devkitARM",
)

new_tarball_dependency(
    name = "devkitarm_crtls",
    build_file = "//deps:devkitarm_crtls.bazel",
    path = "//deps/tarballs:devkitarm-crtls-1.2.6-1-any.pkg.tar.zst",
    strip_prefix = "opt/devkitpro/devkitARM/arm-none-eabi/lib",
)

new_tarball_dependency(
    name = "libgba",
    build_file = "//deps:libgba.bazel",
    path = "//deps/tarballs:libgba-0.5.4-1-any.pkg.tar.zst",
    strip_prefix = "opt/devkitpro/libgba",
)

# --------------------------------------------------------------------------------------------------
# MARK: libtonc

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

# TODO: patch to add "// IWYU pragma: export" on includes
http_archive(
    name = "libtonc",
    build_file = "//deps:libtonc.bazel",
    patch_cmds = [
        # Libtonc exports all its headers from `tonc.h`.
        # This patch avoids `misc-include-cleaner` lint error.
        "sed -E -i '' 's|(#include \"[A-Za-z0-9_./-]+\\.h\")|\\1 // IWYU pragma: export|g' include/tonc.h",
    ],
    sha256 = "d326c1b2b982f1375ff8c9118f463e75af8fccdd23916d7e98b1a3bbfbcffac0",
    strip_prefix = "libtonc-1.4.5",
    urls = ["https://github.com/devkitPro/libtonc/archive/refs/tags/v1.4.5.tar.gz"],
)
